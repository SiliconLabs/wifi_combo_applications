TRUE = 1
FALSE = 0

; User must configure this.
phone = '80-C9-55-B7-E9-3C' ;'B4-CB-57-C0-CA-72'
module_name = 'RS9116W_BT_SECONDARY'

message = 'This example demonstrates BT SPP Secondary functionality in SPP loop back test '

messagebox message 'BT SPP Secondary Example' 

send '|'
wait 'U'
sendln 'U'
mpause 200
sendln '1'
wait 'Loading Done'
message = 'Firmware Loading Done Successfully' 
messagebox message ''


sendln 'at+rsi_opermode=327680,0,1,2147483648,2150629376,1073741824'                ; Set operating mode
wait 'OK'

mpause 200
sendln 'at+rsibt_getlocalbdaddr?'                                                   ; Fetching localbd address
wait 'OK'

mpause 200
sendln 'at+rsibt_setconnmode=1'                                                     ; Setting connection mode
wait 'OK'

mpause 200
sendln 'at+rsibt_setdiscvmode=1'                                                    ; Enabling discoverable mode
wait 'OK'

mpause 200
sendln 'at+rsibt_setprofilemode=1'                                                  ; Setting profile mode
wait 'OK'

mpause 200
sendln 'at+rsibt_setlocalname=20,'module_name                                       ; Setting local name
wait 'OK'

waitln 'AT+RSIBT_BONDRESP'
strsplit inputstr " "

Mac_address = groupmatchstr2
strsplit Mac_address ","
Remote_address = groupmatchstr1

strdim dialog 2
dialog[0] = 'Send Usrpincode Response'
dialog[1] = 'Send Userlinkkey Response'

while TRUE
    listbox 'Connect to the module using your phone.'#10'Respond to AT+RSIBT_USRPINCODEREQ with Option 1.'#10'Respond to AT+RSIBT_USRLINKKEYREQ with Option 2.'#10'When prompted, enter the pin 1234 on your phone.'#10'Press "Byte stream mode" on your phone to chat' 'Choose an Action' dialog

    if result == 0 then
        sendln 'at+rsibt_usrpincode='Remote_address',1,1234'                         ; User pincode response
		break
    elseif result == 1 then
        sendln 'at+rsibt_usrlinkkey='Remote_address',0,1234'                         ; User linkkey response
    else
        break
    endif
endwhile

spp_secondary_receive_data=0
spp_secondary_send_data=0

while TRUE	
	waitln 'AT+RSIBT_SPPRX'
	strsplit inputstr ","
	
	spp_secondary_receive_data = spp_secondary_receive_data + 1
	sprintf2  msg '%d' spp_secondary_receive_data
	dispstr 'spp_secondary received_data count:' msg#13#10#13#10
	
	mpause 500
	
	sendln 'at+rsibt_spptx=28,'groupmatchstr2''
	
	spp_secondary_send_data = spp_secondary_send_data + 1
	sprintf2  msg '%d' spp_secondary_send_data
	dispstr 'spp_secondary send_data count:' msg#13#10#13#10
	
	mpause 500
endwhile

	
:error
messagebox 'An error occured, Please check your configuration and try again''ERROR'

:endscript
dispstr 'BT spp loopback test is completed' #13#10#13#10
end
